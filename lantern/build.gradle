plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                abiFilters rootProject.ext.abiFilters.split(",")
                if (rootProject.ext.useCppSharedLibrary) {
                    arguments "-DANDROID_STL=c++_shared"
                } else {
                    arguments "-DANDROID_STL=c++_static"
                }
                if(rootProject.ext.useASAN) {
                    arguments "-DANDROID_ARM_MODE=arm"
                    arguments "-DUSEASAN=ON"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version rootProject.ext.cmakeVersion
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    buildFeatures {
        prefab true
        prefabPublishing true
    }
    prefab {
        lantern {
            headers "src/main/cpp/include"
        }
    }
}

dependencies {
    implementation 'io.github.hexhacking:xdl:2.0.0'
    implementation 'com.bytedance:bytehook:1.0.8'
}

apply from: rootProject.file('gradle/check.gradle')
apply from: rootProject.file('gradle/publish.gradle')
